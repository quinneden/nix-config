{ config, ... }:
let
  mkGmail =
    {
      email,
      passwordFile,
      default ? false,
    }:
    {
      enable = true;
      settings = {
        inherit default email;
        display-name = "Quinn Edenfield";
        downloads-dir = "/home/quinn/Downloads";
        folder.aliases = {
          inbox = "INBOX";
          sent = "[Gmail]/Sent Mail";
          drafts = "[Gmail]/Drafts";
          trash = "[Gmail]/Trash";
        };
        backend = {
          type = "imap";
          host = "imap.gmail.com";
          port = 993;
          login = email;
          encryption.type = "tls";
          auth.type = "password";
          auth.cmd = "cat ${passwordFile}";
        };
        message.send.backend = {
          type = "smtp";
          host = "smtp.gmail.com";
          port = 465;
          login = email;
          encryption.type = "tls";
          auth.type = "password";
          auth.cmd = "cat ${passwordFile}";
        };
      };
    };
in
{
  programs.himalaya.enable = true;

  accounts.email.accounts = {
    qedenfield.himalaya = mkGmail {
      email = "qedenfield@gmail.com";
      passwordFile = config.sops.secrets."passwords/himalaya-qedenfield".path;
    };

    quinnyxboy.himalaya = mkGmail {
      email = "quinnyxboy@gmail.com";
      passwordFile = config.sops.secrets."passwords/himalaya-quinnyxboy".path;
    };
  };
}
